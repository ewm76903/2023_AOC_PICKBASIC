PROGRAM DAY_4
*
OPEN 'UNIX.FILE' TO F.UNIX ELSE
  CRT 'CANT OPEN UNIX.FILE'
  STOP
END
*
IDATA=""
READ IDATA FROM F.UNIX,'DAY_4' ELSE
  CRT 'NO DAY_4 DATA IN UNIX.FILE'
  STOP
END
*
   RAW.DATA = ''
*
   RAW.DATA<-1> = 'Card 1: 41 48 83 86 17 | 83 86  6 31 17  9 48 53'
   RAW.DATA<-1> = 'Card 2: 13 32 20 16 61 | 61 30 68 82 17 32 24 19'
   RAW.DATA<-1> = 'Card 3: 1 21 53 59 44 | 69 82 63 72 16 21 14  1'
   RAW.DATA<-1> = 'Card 4: 41 92 73 84 69 | 59 84 76 51 58  5 54 83'
   RAW.DATA<-1> = 'Card 5: 87 83 26 28 32 | 88 30 70 12 93 22 82 36'
   RAW.DATA<-1> = 'Card 6: 31 18 13 56 72 | 74 77 10 23 35 67 36 11'
*
*ONLY USE RAW.DATA TO TEST LOGIC
*IDATA=RAW.DATA
*
CRT 'Starting DAY_3 PART 1'
CRT '======================'
ACNT=DCOUNT(IDATA,@AM)
*
GTOT=0
*
FOR I = 1 TO ACNT
  ILINE=""
  ILINE=IDATA<I>
  ILINE=FIELD(ILINE,':',2)  ;*REMOVE "Card #:"
  ILINE=TRIM(ILINE," ","D") ;*REMOVE EXECSS SPACES, AND LEADING AND TRAILING SPACES
  *
  PTOT=0
  FNUM="";SNUM=""
  FNUM=FIELD(ILINE,'|',1)
  SNUM=FIELD(ILINE,'|',2)
  FNUM=TRIM(FNUM,' ','D')
  SNUM=TRIM(SNUM,' ','D')
  CONVERT " " TO @VM IN FNUM
  CONVERT " " TO @VM IN SNUM
  VCNT=DCOUNT(FNUM<1>,@VM)
  FOR K = 1 TO VCNT  ;*CHECK OUR # TO WINING IN FNUM
    THENUM=""
    THENUM=FNUM<1,K>
    LOCATE(THENUM,SNUM,1;THEPOS) THEN
      IF PTOT=0 THEN
        PTOT=1
      END ELSE
        PTOT=PTOT*2
      END
    END
  NEXT K
  *
  IF PTOT # 0 THEN
    GTOT+=PTOT
  END
  *
NEXT I
*
CRT 'GTOT = ':GTOT
*
CRT 'Starting DAY_3 PART 2'
CRT '======================'
ACNT=DCOUNT(IDATA,@AM)
*
GTOT=0
GARR=""
FOR I = 1 TO ACNT
  GARR<I>=1
NEXT I
*
DONE=0
LINENUM=0
LOOP
  LINENUM+=1
UNTIL LINENUM GT ACNT DO
  ILINE=""
  ILINE=IDATA<LINENUM>
  CRT "WORKING ON ":LINENUM:", NUMBER OF CARDS = ":GARR<LINENUM>
  ILINE=FIELD(ILINE,':',2)  ;*REMOVE "Card #:"
  ILINE=TRIM(ILINE," ","D") ;*REMOVE EXECSS SPACES, AND LEADING AND TRAILING SPACES
  *
  PTOT=0
  FNUM="";SNUM=""
  FNUM=FIELD(ILINE,'|',1)
  SNUM=FIELD(ILINE,'|',2)
  FNUM=TRIM(FNUM,' ','D')
  SNUM=TRIM(SNUM,' ','D')
  CONVERT " " TO @VM IN FNUM
  CONVERT " " TO @VM IN SNUM
  VCNT=DCOUNT(FNUM<1>,@VM)
  FOR K = 1 TO VCNT  ;*CHECK OUR # TO WINING IN FNUM
    THENUM=""
    THENUM=FNUM<1,K>
    LOCATE(THENUM,SNUM,1;THEPOS) THEN
      PTOT+=1
    END
  NEXT K
  *
  IF PTOT # 0 THEN
    FOR K = 1 TO PTOT
      GARR<LINENUM+K> += 1 * GARR<LINENUM>
    NEXT K
  END
  *
REPEAT
*
FOR I = 1 TO ACNT
  GTOT+=GARR<I>
NEXT I
*
CRT 'GTOT = ':GTOT
*
STOP
END
